plugins {
    id 'com.enonic.xp.app' version '3.6.1'
    id "com.github.node-gradle.node" version '7.1.0'
}

app {
    name = project.appName
    displayName = 'PWA Starter Kit'
    vendorName = 'Enonic AS'
    vendorUrl = 'https://enonic.com'
    systemVersion = "${xpVersion}"
}

node {
    download = true
    version = '20.10.0'
}

dependencies {
    include "com.enonic.xp:lib-repo:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-websocket:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include 'com.enonic.lib:lib-notifications:2.0.1'
    include "com.enonic.lib:lib-router:3.1.0"
    include "com.enonic.lib:lib-thymeleaf:2.1.1"
    include "com.enonic.lib:lib-mustache:2.1.1"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
}

tasks.register('lint', NpmTask) {
    dependsOn ( npmInstall )
    group "verification"
    args = ['run', 'lint']
    outputs.upToDateWhen { false }
}

tasks.register('webpack', NodeTask) {
    dependsOn( npmInstall )
    script = file('node_modules/webpack/bin/webpack.js')
}

tasks.register('dev', Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'gradlew.bat', 'deploy', '-t'
    } else {
        commandLine './gradlew', 'deploy', '-t'
    }
}

check.dependsOn( lint )

jar {
    exclude 'assets/fonts/material.icons.woff2'
    exclude 'assets/bundles/css/*.js'
    exclude 'assets/bundles/js/*.css'

    includeEmptyDirs = false
    dependsOn( webpack )
}
